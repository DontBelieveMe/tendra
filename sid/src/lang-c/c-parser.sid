/*
 * C Information file grammar.
 */

/*
 * Copyright (c) 2008 The TenDRA Project <http://www.tendra.org/>.
 * All rights reserved.
 *
 * Redistribution and use in source and binary forms, with or without
 * modification, are permitted provided that the following conditions are met:
 *
 * 1. Redistributions of source code must retain the above copyright notice,
 *    this list of conditions and the following disclaimer.
 * 2. Redistributions in binary form must reproduce the above copyright notice,
 *    this list of conditions and the following disclaimer in the documentation
 *    and/or other materials provided with the distribution.
 * 3. Neither the name of The TenDRA Project nor the names of its contributors
 *    may be used to endorse or promote products derived from this software
 *    without specific, prior written permission.
 *
 * THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS ``AS
 * IS'' AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO,
 * THE IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR
 * PURPOSE ARE DISCLAIMED.  IN NO EVENT SHALL THE COPYRIGHT HOLDERS OR
 * CONTRIBUTORS BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL,
 * EXEMPLARY OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO,
 * PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS;
 * OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY,
 * WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR
 * OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF
 * ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
 *
 * $Id$
 */

/*
    		 Crown Copyright (c) 1997

    This TenDRA(r) Computer Program is subject to Copyright
    owned by the United Kingdom Secretary of State for Defence
    acting through the Defence Evaluation and Research Agency
    (DERA).  It is made available to Recipients with a
    royalty-free licence for its use, reproduction, transfer
    to other parties and amendment for any purpose not excluding
    product development provided that any such use et cetera
    shall be deemed to be acceptance of the following conditions:-

        (1) Its Recipients shall ensure that this Notice is
        reproduced upon any copies or amended versions of it;

        (2) Any amended version of it shall be clearly marked to
        show both the nature of and the organisation responsible
        for the relevant amendment or amendments;

        (3) Its onward transfer from a recipient to another
        party shall be deemed to be that party's acceptance of
        these conditions;

        (4) DERA gives no warranty or assurance as to its
        quality or suitability for any purpose and DERA accepts
        no liability whatsoever in relation to any use to which
        it may be put.
*/

%types%

	StringT;
	CCodeP;
	BoolT;

%terminals%

	blt-prefixes;       // %prefixes%
	blt-maps;           // %maps%
	blt-persistents;    // %persistents%
	blt-assignments;    // %assignments%
	blt-terminals;      // %terminals%
	blt-header;         // %header%
	blt-actions;        // %actions%
	blt-trailer;        // %trailer%
	blt-result-assign;  // %result-assignments%
	sid-identifier: () -> (:StringT);
	c-identifier: () -> (:StringT);
	separator;          // ,
	typemark;           // :
	terminator;         // ;
	begin-action;       // <
	define;             // =
	end-action;         // >
	!code: () -> (:CCodeP); // @{ ... @}
	arrow;              // ->
	open-tuple;         // (
	close-tuple;        // )
	blt-param-assign;   // %parameter-assignments%
	reference;          // &
	eof;

	act-at;             // @@
	act-exception;      // @!
	act-terminal;       // @.
	act-advance;        // @>
	act-label:      () -> (:StringT); // @:[a-zA-Z][a-zA-Z0-9]*
	act-reference:  () -> (:StringT); // @&[a-zA-Z][a-zA-Z0-9]*
	act-identifier: () -> (:StringT); // @[a-zA-Z][a-zA-Z0-9]*
	act-modifiable: () -> (:StringT); // @=[a-zA-Z][a-zA-Z0-9]*
	act-codestring: () -> (:StringT); // [^@]*
	act-eof;            //
	act-codestart;      // @{
	act-codeend;        // @}

%productions%

	<set-prefix>: (:StringT &) -> ();
	<x-set-prefix>: (:StringT &) -> ();
	<set-persistent>: (:StringT &, :StringT &) -> () ;
	<empty-string>: () -> (:StringT) ;
	<set-map>: (:StringT &) -> ();
	<x-set-map>: (:StringT &) -> ();
	<init-tuple>;
	<tuple-type>: (:StringT &, :StringT &) -> ();
	<tuple-ref-type>: (:StringT &, :StringT &) -> ();
	<tuple-name>: (:StringT &) -> ();
	<save-tuple>;
	<null-type>;
	<assign>: (:StringT &) -> ();
	<x-assign>: (:CCodeP) -> ();
	<passign>: (:StringT &) -> ();
	<x-passign>: (:CCodeP) -> ();
	<rassign>: (:StringT &) -> ();
	<x-rassign>: (:CCodeP) -> ();
	<set-terminal>: (:StringT &) -> ();
	<x-set-terminal>: (:CCodeP) -> ();
	<set-action>: (:StringT &) -> ();
	<x-set-action>: (:CCodeP) -> ();
	<set-header1>: (:CCodeP) -> ();
	<set-header2>: (:CCodeP) -> ();
	<set-trailer1>: (:CCodeP) -> ();
	<set-trailer2>: (:CCodeP) -> ();

	<ccode-init>: () -> (:CCodeP) ;
	<append-ccode-at>: (:CCodeP) -> () ;
	<append-ccode-exception>: (:CCodeP) -> () ;
	<append-ccode-terminal>: (:CCodeP) -> () ;
	<append-ccode-advance>: (:CCodeP) -> () ;
	<append-ccode-label>: (:CCodeP, :StringT &) -> () ;
	<append-ccode-reference>: (:CCodeP, :StringT &) -> () ;
	<append-ccode-identifier>: (:CCodeP, :StringT &) -> () ;
	<append-ccode-modifiable>: (:CCodeP, :StringT &) -> () ;
	<append-ccode-codestring>: (:CCodeP, :StringT &) -> () ;
	<E-eof-in-code-block>;

	<unhandled-syntax-error>;

	<expected-identifier>;
	<expected-c-identifier>;
	<expected-separator>;
	<expected-open-tuple>;
	<expected-close-tuple>;
	<expected-arrow>;
	<expected-terminator>;
	<expected-end-action>;
	<expected-define>;
	<expected-code>;
	<expected-blt-header>;
	<expected-blt-terminals>;
	<expected-blt-actions>;
	<expected-blt-trailer>;
	<expected-eof>;

	<destroy-string>: (:StringT &) -> ();

	<skip-to-end-of-tuple-defn>;
	<skip-to-end-of-prefix>;
	<skip-to-end-of-map>;
	<skip-to-end-of-assignment>;
	<skip-to-end-of-result-assign>;
	<skip-to-end-of-param-assign>;
	<skip-to-end-of-terminal>;
	<skip-to-end-of-action>;
	<skip-recover>;

	<is-close-tuple-or-skipped-or-eof>: () -> (:BoolT);

	c-parse-grammar [
		/*
		 * Identifiers
		 */

		identifier: () -> (id :StringT) = {
			id = sid-identifier;
		||
			id = c-identifier;
		};

		code-block: () -> (c :CCodeP) [
			code-body-single: (c :CCodeP) -> () = {
				act-at;
				<append-ccode-at>(c) ;
			||
				act-exception;
				<append-ccode-exception>(c) ;
			||
				act-terminal;
				<append-ccode-terminal>(c) ;
			||
				act-advance;
				<append-ccode-advance>(c) ;
			||
				i = act-label;
				<append-ccode-label>(c, &i) ;
			||
				i = act-reference;
				<append-ccode-reference>(c, &i) ;
			||
				i = act-identifier;
				<append-ccode-identifier>(c, &i) ;
			||
				i = act-modifiable;
				<append-ccode-modifiable>(c, &i) ;
			||
				s = act-codestring;
				<append-ccode-codestring>(c, &s) ;
			||
				act-eof;
				<E-eof-in-code-block>;
			};

			code-body: (c :CCodeP) -> () = {
				$;
			||
				code-body-single(c);
				code-body(c);
			};
		] = {
			act-codestart ;
			c = <ccode-init>;
			code-body(c);
			act-codeend ;
		};


		/*
		 * Prefix definitions
		 */

		prefix-list [
			prefix = {
				prefix = identifier;
				<set-prefix>(&prefix);

				{
					define;
				##
					<expected-define>;
				};

				{
					name = c-identifier;
					<x-set-prefix>(&name);

					{
						terminator;
					##
						<expected-terminator>;
					};
				##
					<expected-c-identifier>;
					<skip-to-end-of-prefix>;
				};
			};
		] = {
			$;
		||
			prefix;
			<skip-recover>;
			prefix-list;
		};


		/*
		 * Identifier mapping
		 */

		map-list [
			map = {
				from = identifier;
				<set-map>(&from);

				{
					arrow;
				##
					<expected-arrow>;
				};

				{
					to = c-identifier;
					<x-set-map>(&to);

					{
						terminator;
					##
						<expected-terminator>;
					};
				##
					<expected-c-identifier>;
					<skip-to-end-of-map>;
				};
			};
		] = {
			$;
		||
			map;
			<skip-recover>;
			map-list;
		};

		persistent-list [
			persistent = {
				{
					name = c-identifier;
				##
					name = <empty-string>;
					<expected-c-identifier>;
				};

				typemark;

				{
					type = c-identifier;
				##
					type = <empty-string>;
					<expected-identifier>;
				};

				<set-persistent>(&name, &type);
				{
					terminator;
				##
					<expected-terminator>;
				};
			};
		] = {
			$;
		||
			persistent;
			persistent-list;
		};


		/*
		 * Function type specifications
		 */

		function-type-defn [
			tuple-defn = {
				name = c-identifier;

				{
					typemark;

					{
						type = identifier;

						{
							reference;
							<tuple-ref-type>(&name, &type);
						||
							<tuple-type>(&name, &type);
						};
					##
						<expected-identifier>;
						<destroy-string>(&name);
						<skip-to-end-of-tuple-defn>;
					};
				||
					<tuple-name>(&name);
				};
			##
				<expected-identifier>;
				<skip-to-end-of-tuple-defn>;
			};

			tuple-defn-list-1 = {
				tuple-defn;

				{
					? = <is-close-tuple-or-skipped-or-eof>;
				||
					separator;
					tuple-defn-list-1;
				##
					<expected-separator>;
					tuple-defn-list-1;
				};
			};

			tuple-defn-list = {
				$;
			||
				tuple-defn-list-1;
			};

			type-tuple-defn = {
				<init-tuple>;

				{
					open-tuple;
				##
					<expected-open-tuple>;
				};

				tuple-defn-list;
				<skip-recover>;

				{
					close-tuple;
				##
					<expected-close-tuple>;
				};
			};
		] = {
			typemark;
			type-tuple-defn;
			<save-tuple>;

			{
				arrow;
			##
				<expected-arrow>;
			};

			type-tuple-defn;
		||
			<null-type>;
		};


		/*
		 * Assignment definitions
		 */

		assignment-list [
			assignment = {
				id = identifier;
				<assign>(&id);
				function-type-defn;

				{
					define;
				##
					<expected-define>;
				};

				{
					body = code-block;
					<x-assign>(body);

					{
						terminator;
					##
						<expected-terminator>;
					};
				##
					<expected-code>;
					<skip-to-end-of-assignment>;
				};
			};
		] = {
			$;
		||
			assignment;
			<skip-recover>;
			assignment-list;
		};

		param-assign-list [
			param-assign = {
				id = identifier;
				<passign>(&id);
				function-type-defn;

				{
					define;
				##
					<expected-define>;
				};

				{
					body = code-block;
					<x-passign>(body);

					{
						terminator;
					##
						<expected-terminator>;
					};
				##
					<expected-code>;
					<skip-to-end-of-param-assign>;
				};
			};
		] = {
			$;
		||
			param-assign;
			<skip-recover>;
			param-assign-list;
		};

		result-assign-list [
			result-assign = {
				id = identifier;
				<rassign>(&id);
				function-type-defn;

				{
					define;
				##
					<expected-define>;
				};

				{
					body = code-block;
					<x-rassign>(body);

					{
						terminator;
					##
						<expected-terminator>;
					};
				##
					<expected-code>;
					<skip-to-end-of-result-assign>;
				};
			};
		] = {
			$;
		||
			result-assign;
			<skip-recover>;
			result-assign-list;
		};


		/*
		 * Terminal result extraction definitions
		 */

		terminal-list [
			terminal = {
				id = identifier;
				<set-terminal>(&id);
				function-type-defn;

				{
					define;
				##
					<expected-define>;
				};

				{
					body = code-block;
					<x-set-terminal>(body);

					{
						terminator;
					##
						<expected-terminator>;
					};
				##
					<expected-code>;
					<skip-to-end-of-terminal>;
				};
			};
		] = {
			$;
		||
			terminal;
			<skip-recover>;
			terminal-list;
		};


		/*
		 * Action definitions
		 */

		action-list [
			action = {
				begin-action;

				{
					id = identifier;
					<set-action>(&id);

					{
						end-action;
					##
						<expected-end-action>;
					};

					function-type-defn;

					{
						define;
					##
						<expected-define>;
					};

					{
						body = code-block;
						<x-set-action>(body);

						{
							terminator;
						##
							<expected-terminator>;
						};
					##
						<expected-code>;
						<skip-to-end-of-action>;
					};
				##
					<expected-identifier>;
					<skip-to-end-of-action>;
				};
			};
		] = {
			$;
		||
			action;
			<skip-recover>;
			action-list;
		};
	] /* c-parse-grammar */ = {
		{
			blt-prefixes;
			prefix-list;
		||
			$;
		};

		{
			blt-persistents;
			persistent-list;
		||
			$;
		};

		{
			blt-maps;
			map-list;
		||
			$;
		};

		{
			blt-header;
		##
			<expected-blt-header>;
		};

		{
			h1 = code-block;
			<set-header1>(h1);
		##
			<expected-code>;
		};

		{
			separator;
		##
			<expected-separator>;
		};

		{
			h2 = code-block;
			<set-header2>(h2);
		##
			<expected-code>;
		};

		{
			terminator;
		##
			<expected-terminator>;
		};

		{
			blt-assignments;
			assignment-list;
		||
			$;
		};

		{
			blt-param-assign;
			param-assign-list;
		||
			$;
		};

		{
			blt-result-assign;
			result-assign-list;
		||
			$;
		};

		{
			blt-terminals;
		##
			<expected-blt-terminals>;
		};

		terminal-list;

		{
			blt-actions;
		##
			<expected-blt-actions>;
		};

		action-list;

		{
			blt-trailer;
		##
			<expected-blt-trailer>;
		};

		{
			t1 = code-block;
			<set-trailer1>(t1);
		##
			<expected-code>;
		};

		{
			separator;
		##
			<expected-separator>;
		};

		{
			t2 = code-block;
			<set-trailer2>(t2);
		##
			<expected-code>;
		};

		{
			terminator;
		##
			<expected-terminator>;
		};

		{
			eof;
		##
			<expected-eof>;
		};
	##
		<unhandled-syntax-error>;
	};

%entry% c-parse-grammar;

