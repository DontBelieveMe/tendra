/* $Id$ */

/*
 * Copyright 2002-2011, The TenDRA Project.
 *
 * See doc/copyright/ for the full copyright terms.
 */

%types%

	BOOL;
	ENTRYP;
	IDENT;
	BIGSTRING;
	PARAM_LIST;
	CODE;

%terminals%

	colon;     // :
	arrow;     // ->
	ref;       // &
	define;    // =
	semicolon; // ;
	comma;     // ,
	open;      // (
	close;     // )

	code-start;	// @{
	code-end;	// @}
	lone-code-at;
	code-at;
	code-eof;

	eof;
	ident:       () -> (:IDENT);
	code-ident:  () -> (:IDENT);
	code-ref:    () -> (:IDENT);
	code-string: () -> (:BIGSTRING);
	!unknown;

	kw-header;
	kw-trailer;
	kw-action;
	kw-argument;

%productions%

	<true>:  () -> (:BOOL);
	<false>: () -> (:BOOL);

	<empty-ident>: () -> ( : IDENT);

	<set-headers>:  (:CODE, :CODE) -> ();
	<set-trailers>: (:CODE, :CODE) -> ();

	<init-param>:   () -> (:PARAM_LIST);
	<append-param>: (:PARAM_LIST &, :IDENT, :IDENT, :BOOL) -> ();

	<define-action>:       (:ENTRYP, :CODE) -> ();
	<check-action-params>: (:ENTRYP, :PARAM_LIST, :PARAM_LIST) -> ();
	<get-entry-action>:    (:IDENT) -> (:ENTRYP);
	<null-entry>:          () -> (:ENTRYP);

	<init-code>:          () -> (:CODE);
	<append-code-string>: (:CODE &, :BIGSTRING &) -> ();
	<append-code-ident>:  (:CODE &, :IDENT, :ENTRYP, :BOOL) -> ();
	<append-code-at>:     (:CODE &) -> ();

	<define-additional-argument>: (:IDENT, :IDENT) -> ();

	<E-syntax-error>;
	<E_expected_code_end>;
	<E_expected_code_start>;
	<E_expected_comma>;
	<E_expected_define>;
	<E_expected_arrow>;
	<E_expected_colon>;
	<E_expected_semicolon>;
	<E_expected_ident>;

	<E_unexpected_eof>;
	<E_lone_at_in_code_block>;

	code-block: (e :ENTRYP) -> (c :CODE) [

		code-element: (c :CODE &, e :ENTRYP) -> () = {
			s = code-string;
			<append-code-string>(&c, &s);
		||
			i = code-ident;
			b = <false>;
			<append-code-ident>(&c, i, e, b);
		||
			i = code-ref;
			b = <true>;
			<append-code-ident>(&c, i, e, b);
		||
			lone-code-at;
			<E_lone_at_in_code_block>;
		||
			code-at;
			<append-code-at>(&c);
		};

		code-body: (c :CODE &, e :ENTRYP) -> () = {
			$;
		||
			code-element(&c, e);
			code-body(&c, e);
		};

	] = {
		{
			code-start;
		##
			<E_expected_code_start>;
		};

		c = <init-code>;
		code-body(&c, e);

		{
			$;
		||
			code-eof;
			<E_unexpected_eof>;
		};

		{
			code-end;
		##
			<E_expected_code_end>;
		};
	};

	header-stmnt: () -> () = {
		kw-header;
		{
			define;
		##
			<E_expected_define>;
		};

		e = <null-entry>;

		sc = code-block(e);
		{
			comma;
		##
			<E_expected_comma>;
		};
		sh = code-block(e);

		<set-headers>(sc, sh);

		{
			semicolon;
		##
			<E_expected_semicolon>;
		};
	};

	trailer-stmnt: () -> () = {
		kw-trailer;
		{
			define;
		##
			<E_expected_define>;
		};

		e = <null-entry>;
		sc = code-block(e);

		{
			comma;
		##
			<E_expected_comma>;
		};

		sh = code-block(e);
		<set-trailers>(sc, sh);

		{
			semicolon;
		##
			<E_expected_semicolon>;
		};
	};

	action-definition: () -> () [

		param-list: ( ) -> (a :PARAM_LIST) [

			param: (a :PARAM_LIST &) -> () = {
				{
					n = ident;
				||
					n = <empty-ident>;
				};

				colon;
				t = ident;

				{
					isref = <false>;
				||
					ref;
					isref = <true>;
				};

				<append-param>(&a, n, t, isref);
			};

			param-list1: (a :PARAM_LIST &) -> () = {
				param(&a);
			||
				param(&a);
				comma;
				param-list1(&a);
			};

		] = {
			open;
			a = <init-param>;
			close;
		||
			open;
			a = <init-param>;
			param-list1(&a);
			close;
		};

		function-type-defn: () -> (in :PARAM_LIST, out :PARAM_LIST) = {
			in  = <init-param>;
			out = <init-param>;
		||
			colon;
			in = param-list;
			{
				arrow;
			##
				<E_expected_arrow>;
			};
			out = param-list;
		};
	] = {

		kw-action;
		{
			n = ident;
		##
			n = <empty-ident>;
			<E_expected_ident>;
		};

		(in, out) = function-type-defn;

		{
			define;
		##
			<E_expected_define>;
		};

		ea = <get-entry-action>(n);
		<check-action-params>(ea, in, out);
		c = code-block(ea);
		<define-action>(ea, c);

		{
			semicolon;
		##
			<E_expected_semicolon>;
		};
	};

	argument-stmnt: () -> () = {
		kw-argument;
		{
			name = ident;
		##
			name = <empty-ident>;
			<E_expected_ident>;
		};

		{
			colon;
		##
			<E_expected_colon>;
		};

		{
			ctype = ident;
		##
			ctype = <empty-ident>;
			<E_expected_ident>;
		};

		<define-additional-argument>(name, ctype);
		{
			semicolon;
		##
			<E_expected_semicolon>;
		};
	};


	statement: () -> () = {
		header-stmnt;
	||
		trailer-stmnt;
	||
		action-definition;
	||
		argument-stmnt;
	};

	statement-list: () -> () = {
		$;
	||
		statement; statement-list;
	};

	lct-unit: () -> () = {
		statement-list;
		eof;
	##
		<E-syntax-error>;
	};

%entry%

	lct-unit;

