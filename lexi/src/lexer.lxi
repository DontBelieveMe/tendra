/* $Id$ */

/*
 * Copyright 2002-2011, The TenDRA Project.
 * Copyright 1997, United Kingdom Secretary of State for Defence.
 *
 * See doc/copyright/ for the full copyright terms.
 */

/*
 * LEXICAL ANALYSER FOR LEXI
 *
 * This file describes the lexical analyser used by lexi.
 */

ACTION <init_tokbuf>;
ACTION <push_tokbuf>: (:CHARACTER) -> ();
ACTION <fini_tokbuf>;
ACTION <E_eof_in_string>;
ACTION <E_newline_in_string>;

ACTION <keyword>: (:TERMINAL) -> (:TERMINAL);

ACTION <numbuf_init>: () -> ();
ACTION <numbuf_push_digit>: (:CHARACTER) -> ();

GROUP white   = " \t\n\r";
GROUP alpha   = {A-Z} + {a-z} + "_";
GROUP digit   = {0-9};
GROUP alnum   = "[alpha][digit]";
GROUP alnumhy = "[alnum]-";

TOKEN "("  -> $open;
TOKEN ")"  -> $close;
TOKEN "<"  -> $begin-action;
TOKEN ">"  -> $end-action;
TOKEN "{"  -> $open-brace;
TOKEN "}"  -> $close-brace;
TOKEN "->" -> $arrow;
TOKEN ":"  -> $colon;
TOKEN "&"  -> $ref;
TOKEN ";"  -> $semicolon;
TOKEN "="  -> $equal;
TOKEN "+"  -> $plus;
TOKEN "$$" -> $discard;
TOKEN ","  -> $comma;
TOKEN "\e" -> $eof;

TOKEN "..."    -> $range;
TOKEN "\[...]" -> $range-closed-closed;
TOKEN "\[...)" -> $range-closed-open;

TOKEN "#n"     -> $arg-char-count;
TOKEN "#*"     -> $arg-char-string;
TOKEN "$"      -> $arg-return;
TOKEN "!"      -> $arg-ignore;

TOKEN "{A-Z}" -> $upper;
TOKEN "{a-z}" -> $lower;
TOKEN "{0-9}" -> $digit;

ZONE arg_char_num:
	"#[digit]" -> <numbuf_init>, <numbuf_push_digit>(#1) [...)
	"[^digit]" -> $arg-char-num
{
	TOKEN DEFAULT -> <numbuf_push_digit>(#0);
}

ZONE comment: "/*" ... "*/" {
	GROUP white = "";
	TOKEN DEFAULT -> $$;
}

ZONE linecomment: "//" ... "\n" {
	GROUP white = "";
	TOKEN DEFAULT -> $$;
}

ZONE string: "\"" -> <init_tokbuf> ... "\"" -> <fini_tokbuf>, $string {
	GROUP white = "";
	TOKEN DEFAULT -> <push_tokbuf>(#0);
	TOKEN "\\\""  -> <push_tokbuf>(#1);
	TOKEN "\\\\"  -> <push_tokbuf>(#0), <push_tokbuf>(#0);
	TOKEN "\e"    -> <E_eof_in_string>;
	TOKEN "\n"    -> <E_newline_in_string>;
}

ZONE ident:
	"[alpha]"  -> <init_tokbuf>, <push_tokbuf>(#0) [...)
	"[^alnum]" -> <fini_tokbuf>, $ = <keyword>($ident)
{
	GROUP white = "";
	TOKEN DEFAULT -> <push_tokbuf>(#0);
}

ZONE sidident:
	"$[alpha]"   -> <init_tokbuf>, <push_tokbuf>(#1) [...)
	"[^alnumhy]" -> <fini_tokbuf>, $sid-ident
{
	GROUP white = "";
	TOKEN DEFAULT -> <push_tokbuf>(#0);
}

KEYWORD "TYPE"    -> $kw-type;
KEYWORD "GROUP"   -> $kw-group;
KEYWORD "ACTION"  -> $kw-action;
KEYWORD "KEYWORD" -> $kw-keyword;
KEYWORD "ZONE"    -> $kw-zone;
KEYWORD "MAPPING" -> $kw-mapping;
KEYWORD "TOKEN"   -> $kw-token;

KEYWORD "DEFAULT" -> $default;
KEYWORD "white"   -> $white;

