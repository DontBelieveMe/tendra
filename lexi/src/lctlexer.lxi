/* $Id$ */

/*
 * Copyright 2002-2011, The TenDRA Project.
 *
 * See doc/copyright/ for the full copyright terms.
 */

GROUP alpha = {A-Z} + {a-z} + "_";
GROUP alnum = {A-Z} + {a-z} + {0-9} + "_";

ACTION <init_tokbuf>;
ACTION <push_tokbuf>: (:CHARACTER) -> ();
ACTION <fini_tokbuf>;

ACTION <keyword>: (:TERMINAL)  -> (:TERMINAL);
ACTION <code_string>: (:CHARACTER) -> (:TERMINAL) ;

TOKEN "("  -> $open;
TOKEN ")"  -> $close;
TOKEN "="  -> $define;
TOKEN "&"  -> $ref;
TOKEN ";"  -> $semicolon;
TOKEN ":"  -> $colon;
TOKEN ","  -> $comma;
TOKEN "->" -> $arrow;
TOKEN "\e" -> $eof;

TOKEN DEFAULT -> $unknown;

ZONE ident:
	"[alpha]"  -> <init_tokbuf>, <push_tokbuf>(#0) [...)
	"[^alnum]" -> <fini_tokbuf>, $ = <keyword>($ident)
{
	GROUP white = "";
	TOKEN DEFAULT -> <push_tokbuf>(#0);
}

ZONE comment: "/*" ... "*/" {
	GROUP white = "";
	TOKEN DEFAULT -> $$;
}

ZONE linecomment: "//" ... "\n" {
	GROUP white = "";
	TOKEN DEFAULT -> $$;
}

ZONE code: "@{" -> $code-start ... "@}" -> $code-end {
	GROUP white = "";
	TOKEN DEFAULT -> $ = <code_string>(#0);

	ZONE code_ident:
		"@[alpha]" -> <init_tokbuf>, <push_tokbuf>(#1) [...)
		"[^alnum]" -> <fini_tokbuf>, $code-ident
	{
		TOKEN DEFAULT -> <push_tokbuf>(#0);
	}

	ZONE code_ref:
		"@&[alpha]" -> <init_tokbuf>, <push_tokbuf>(#2) [...)
		"[^alnum]"  -> <fini_tokbuf>, $code-ref
	{
		TOKEN DEFAULT -> <push_tokbuf>(#0);
	}

	TOKEN "@@" -> $code-at;
	TOKEN "@"  -> $lone-code-at;
	TOKEN "\e" -> $code-eof;
}

KEYWORD "HEADERS"  -> $header-kw;
KEYWORD "TRAILERS" -> $trailer-kw;
KEYWORD "MAP"      -> $map-kw;
KEYWORD "ACTION"   -> $action-kw;
KEYWORD "ARGUMENT" -> $argument-kw;

